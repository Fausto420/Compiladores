// - LÉXICO -

// Ignorar espacios y saltos de línea
WS: /[ \t\r\n]+/
%ignore WS

// Expresiones Regulares para Tokens
ID: /[A-Za-z_][A-Za-z_0-9]*/
CTE_FLOAT: /[0-9]+\.[0-9]+/
CTE_INT: /[0-9]+/
CTE_STRING: /"[^"\n]*"/

// Palabras Clave (TODAS EN ESPAÑOL)
PROGRAMA: "programa"
VARS: "vars"
NULA: "nula"
INICIO: "inicio"
FIN: "fin"
SI: "si"
SINO: "sino"
MIENTRAS: "mientras"
HAZ: "haz"
ESCRIBE: "escribe"
RETURN: "return"
ENTERO: "entero"
FLOTANTE: "flotante"

// Operadores y Separadores
IGUAL: "=="
DIFERENTE: "!="
ASIGNA: "="
MAYOR: ">"
MENOR: "<"
MAS: "+"
MENOS: "-"
POR: "*"
ENTRE: "/"
DOS_PUNTOS: ":"
COMA: ","
PUNTO_COMA: ";"

// Agrupadores
PAREN_IZQ: "("
PAREN_DER: ")"
LLAVE_IZQ: "{"
LLAVE_DER: "}"
CORCHETE_IZQ: "["
CORCHETE_DER: "]"

// - SINTAXIS -

start: programa

programa: PROGRAMA ID PUNTO_COMA vars_seccion? funcs_seccion* cuerpo_principal

cuerpo_principal: INICIO LLAVE_IZQ estatutos LLAVE_DER FIN

tipo: ENTERO | FLOTANTE


// SECCIÓN DE VARIABLES

vars_seccion: VARS DOS_PUNTOS declaracion_var+

declaracion_var: lista_ids DOS_PUNTOS tipo PUNTO_COMA

lista_ids: ID (COMA ID)*

// SECCIÓN DE FUNCIONES

funcs_seccion: func_decl*

func_decl: tipo_retorno ID PAREN_IZQ params? PAREN_DER LLAVE_IZQ vars_seccion? estatutos LLAVE_DER PUNTO_COMA

tipo_retorno: NULA | tipo

params: param (COMA param)*
param: ID DOS_PUNTOS tipo

// CUERPO Y ESTATUTOS

estatutos: estatuto*

estatuto: asignacion | condicion | ciclo | llamada_func | imprime | retorno | bloque_anidado

bloque_anidado: CORCHETE_IZQ estatutos CORCHETE_DER

// ASIGNACIÓN

asignacion: ID ASIGNA expresion PUNTO_COMA

// IMPRIME (ESCRIBE)

imprime: ESCRIBE PAREN_IZQ args_imprime PAREN_DER PUNTO_COMA

args_imprime: (expresion (COMA expresion)*) | (CTE_STRING (COMA expresion)?)

// CONDICIONAL

condicion: SI PAREN_IZQ expresion PAREN_DER cuerpo (SINO cuerpo)? PUNTO_COMA

cuerpo: LLAVE_IZQ estatutos LLAVE_DER

// CICLO

ciclo: MIENTRAS PAREN_IZQ expresion PAREN_DER HAZ cuerpo PUNTO_COMA

// LLAMADA A FUNCIÓN

llamada_func: ID PAREN_IZQ args? PAREN_DER PUNTO_COMA

args: expresion (COMA expresion)*

// RETURN

retorno: RETURN expresion? PUNTO_COMA

// EXPRESIONES

expresion: exp_simple cola_relacional?

cola_relacional: (MAYOR | MENOR | DIFERENTE | IGUAL) exp_simple

exp_simple: termino ((MAS | MENOS) termino)*

termino: factor ((POR | ENTRE) factor)*

factor: signo? primario

signo: MAS | MENOS

primario: PAREN_IZQ expresion PAREN_DER | constante | ID sufijo_llamada?

sufijo_llamada: PAREN_IZQ args? PAREN_DER

constante: CTE_INT | CTE_FLOAT

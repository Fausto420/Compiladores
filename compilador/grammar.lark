// - LÉXICO -

// Ignorar espacios y saltos de línea
WS: /[ \t\r\n]+/
%ignore WS

// Regex
ID: /[A-Za-z_][A-Za-z_0-9]*/
CTE_FLOAT: /[0-9]+\.[0-9]+/
CTE_INT: /[0-9]+/
CTE_STRING: /"[^"\n]*"/

// Palabras clave
PROGRAM: "program"
VARS: "vars"
VOID: "void"
MAIN: "main"
END: "end"
IF: "if"
ELSE: "else"
WHILE: "while"
DO: "do"
PRINT: "print"
INT: "int"
FLOAT: "float"

// Operadores y separadores
EQUAL: "=="
NOTEQUAL: "!="
ASSIGN: "="
GREATER: ">"
LESS: "<"
PLUS: "+"
MINUS: "-"
STAR: "*"
SLASH: "/"
COLON: ":"
COMMA: ","
SEMICOL: ";"

// Agrupadores
LPAREN: "("
RPAREN: ")"
LBRACE: "{"
RBRACE: "}"
LBRACKET: "["
RBRACKET: "]"

// - SINTAXIS - 

start: program

// Programa
program: PROGRAM ID SEMICOL vars_section funcs_section MAIN body END

// Tipos
type: INT | FLOAT

// Variables
vars_section: VARS COLON decvar+ 
decvar: id_list COLON type SEMICOL
id_list: ID (COMMA ID)*

// Funciones
funcs_section: func_decl*
func_decl: VOID ID LPAREN params RPAREN LBRACKET vars_section body RBRACKET SEMICOL
params: param (COMMA param)*
param: ID COLON type

// BODY y sentencias
body: LBRACE stmt_list RBRACE
stmt_list: statement*

statement: assign | condition | cycle | fcall | print_stmt | body

// Asignación 
assign: ID ASSIGN expression SEMICOL

// Condicional
condition: IF LPAREN expression RPAREN body (ELSE body)? SEMICOL

// Ciclo While
cycle: WHILE LPAREN expression RPAREN DO body SEMICOL

// Llamada a función
fcall: ID LPAREN args RPAREN SEMICOL
args: expression (COMMA expression)*

// Print
print_stmt: PRINT LPAREN print_args RPAREN SEMICOL
print_args: CTE_STRING | expression (COMMA CTE_STRING)?

// - EXPRESIONES -

?expression: simple_expr rel_tail?
rel_tail: (GREATER | LESS | NOTEQUAL | EQUAL) simple_expr

?simple_expr: term ((PLUS | MINUS) term)*
?term: factor ((STAR | SLASH) factor)*

?factor: LPAREN expression RPAREN | sign_opt primary
sign_opt: (PLUS | MINUS)?

primary: ID | number
number: CTE_INT | CTE_FLOAT
